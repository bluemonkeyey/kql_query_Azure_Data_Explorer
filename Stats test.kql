let stats = datatable(data: dynamic)
[
    dynamic([1000,900,30,50,7,5,3,2,1,4,5,6,5,3,2,12,2,1,4,5,6,7,6,4,3,6,4,3,2,4,5,8,8,7,7,6,7,7,8,9,3,2,1])
];
// Expandimos el array y le damos clave para joins posteriores
let expanded = stats
| mv-expand value = data
| project value = toint(value)
| extend key = 1;
// Moda del conjunto
let moda = expanded
| summarize freq = count() by value
| top 1 by freq desc
| project moda = value
| extend key = 1;
// Estadísticas principales
let estadisticas = expanded
| summarize 
    media = avg(value),
    mediana = percentile(value, 50),
    stdev = stdev(value),
    Q1 = percentile(value, 25),
    Q3 = percentile(value, 75)
| extend 
    tukey_limit = Q3 + 1.5 * (Q3 - Q1),
    key = 1;
// Unimos todo y calculamos detección
expanded
| join kind=inner (estadisticas) on key
| join kind=inner (moda) on key
| project-away key
| extend 
    zscore = (value - media) / stdev
| extend 
    malisioso_iqr = iff(value > tukey_limit, "malo", "no"),
    malisioso_outlier = iff(zscore > 2, "malo", "no"),
    sesgo= iff(media > mediana, "True", "False")
