// this query detects anomalies combining different thresholds for listKeys vs other actions
let relevantActions = dynamic([
    "microsoft.automation/automationaccounts/jobs/write",
    "microsoft.automation/automationaccounts/runbooks/publish/action",
    "microsoft.automation/automationaccounts/runbooks/draft/write",
    "microsoft.automation/automationaccounts/runbooks/write",
    "microsoft.resources/deployments/read"
]);
// set the specific listKeys operation
let listKeysOperation = dynamic(["microsoft.storage/storageaccounts/listkeys/action"]);
// detect unusual activity: user did an operation in [10..7] days but not in [6..0] days
let unusualActivity =
    azureactivity
    | extend resourceIdStr = tostring(parse_json(Properties).resource)
    | where TimeGenerated between (ago(10d) .. ago(7d))
    | summarize lastSeen = max(TimeGenerated) by Caller, OperationNameValue, resourceIdStr
    | join kind=leftanti (
        azureactivity
        | extend resourceIdStr = tostring(parse_json(Properties).resource)
        | where TimeGenerated between (ago(6d) .. now())
        | summarize recentActivity = max(TimeGenerated) by Caller, OperationNameValue, resourceIdStr
    ) on Caller, OperationNameValue, resourceIdStr;
// define caller-resource pairs that are frequent in the last 30 days
let frequentCallerResource =
    azureactivity
    | extend resourceIdStr = tostring(parse_json(Properties).resource)
    | where TimeGenerated between (ago(30d) .. now())
    | where OperationNameValue has_any (relevantActions) or OperationNameValue has_any (listKeysOperation)
    | summarize histCount = count() by Caller, resourceIdStr
    | where histCount > 5;
// define a baseline with two thresholds: one for listKeys, one for other ops
let dualBaseline =
    azureactivity
    | extend resourceIdStr = tostring(parse_json(Properties).resource)
    | where TimeGenerated between (ago(30d) .. now())
    | where OperationNameValue has_any (relevantActions) or OperationNameValue has_any (listKeysOperation)
    | summarize activityCount = count() by Caller, OperationNameValue
    | where iif(
        OperationNameValue has_any (listKeysOperation),
        activityCount > 100, // higher threshold for listKeys
        activityCount > 20    // lower threshold for other actions
    );
// exclude frequent caller-resource pairs in the last 6 days
let frequentExclusions =
    azureactivity
    | extend resourceIdStr = tostring(parse_json(Properties).resource)
    | where TimeGenerated > ago(6d)
    | join kind=inner (frequentCallerResource) on Caller, resourceIdStr
    | project TimeGenerated, Caller, OperationNameValue, resourceIdStr;
// final detection query
azureactivity
| extend resourceIdStr = tostring(parse_json(Properties).resource)
| where TimeGenerated between (ago(6d) .. now())
| where OperationNameValue has_any (relevantActions) or OperationNameValue has_any (listKeysOperation)
// match with unusual activity
| join kind=inner (unusualActivity) on resourceIdStr, OperationNameValue
// count how many times each operation occurred
| summarize operationCount = count() by resourceIdStr, ResourceGroup, Caller, OperationNameValue, Caller1
// exclude any caller-operation that is considered "hyperactive" in dualBaseline
| join kind=leftanti (dualBaseline) on Caller, OperationNameValue
| where Caller != Caller1
// exclude frequent caller-resource pairs
| join kind=leftanti (frequentExclusions) on Caller, resourceIdStr
| summarize by resourceIdStr, ResourceGroup, Caller, OperationNameValue, Caller1
